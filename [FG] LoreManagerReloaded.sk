
#name: LoreManagerReloaded  by FallenGundam
#version: v1.0.2
#date: 2022/4/4
#required:
    #paper or spigot 1.13.2~1.18.2 
    #skript 2.3.7 - lastest
#dc: FallenGundam#8864
 
#command:
    # /loremanager , /lm : open lore manager gui
    #permission: lm.admin


#API:
    #item creator
    # type: item type
    # amount: item amount
    # name: custom name of item
    # lore: string list
    # damage: damage of itme
    # ench: enchant list  https://skripthub.net/docs/?id=2143
    # unb: unbreakable
    #
    # example:
    #    set {_lore::*} to "&aline1" and "&bline2"
    #    set {_enchant::*} to Loyalty and Power
    #    give createitem(iron sword,5,"&ethis is sword",{_lore::*},1,{_enchant::*},true) to player




#> createitem function
#> @param type text 物品id
#> @param amount integer 數量
#> @param name text 物品名稱
#> @param lore strings 物品敘述
#> @param damage integer 物品耐久度
#> @param ench objs 附魔
#> @param unb boolean 是否堅不可摧
#> @return item
function createitem(type:object,amount:integer,name:object,lore:Strings,damage:integer,ench:objects,unb:boolean) :: item:
	set {_item} to type of {_type}
	set item amount of {_item} to {_amount}
	set name of {_item} to {_name}
	set lore of {_item} to {_lore::*}
	enchant {_item} with {_ench::*}
	set durability of {_item} to {_damage}
	if {_unb} is true:
		set {_item} to unbreakable {_item}
	return {_item} 




command /loremanager:
    aliases: lm
    permission: lm.admin
    trigger:
        if player's tool isn't air:
            set {_lore::*} to lore of player's tool
            lore_itemgui(player,{_lore::*})
        else:
            send "&C請手持物品"

on inventory click:
    if name of event-inventory contains "lore管理":
        cancel event
        if type of event-inventory is chest inventory:
            if event-slot isn't air:
                #點擊敘述
                if "%type of event-slot%" is "book":

                    if click action is left mouse button:
                        close player's inventory
                        set {_lore} to line 3 of lore of event-slot
                        send "&f正在編輯 %name of event-slot%"
                        send "&7&m---------------------------------"
                        send "&a請輸入要替換的字串, 輸入<tooltip:cancel><sgt:cancel>&7[&c!&7] &acancel可以取消 "

                        replace all "§" with "&" in {_lore}
                        set {_tellraw} to "{""text"":""&7[&c!&7] &b點擊此處可編輯此字串"",""hoverEvent"":{""action"":""show_text"",""value"":{""text"":""%line 3 of lore of event-slot%""}},""clickEvent"":{""action"": ""suggest_command"",""value"": ""%{_lore}%""}}"
                        execute console command "tellraw %player% %{_tellraw}%"
                        # send "<tooltip:%{_lore}%><sgt:%{_lore}%> &7[&c!&7] &b點擊此處可編輯此字串"
                        send "&7&m---------------------------------"
                        play sound "BLOCK.PISTON.EXTEND" with volume 1 with pitch 1.5 to player
                        set {lorechat::%player%::line} to uncolored name of event-slot
                        replace all "第" and "行" and " " with "" in {lorechat::%player%::line}
                        set {lorechat::%player%::line} to {lorechat::%player%::line} parsed as number
                    else if click action is right mouse button:
                        set {_index} to index of event-slot
                        add "&7&m---------------------------------" to {_lore::*}
                        add "" to {_lore::*}
                        add line 3 of lore of event-slot to {_lore::*}
                        add "" to {_lore::*}
                        add "&7&m---------------------------------" to {_lore::*}
                        set {_a} to "%item amount of event-slot%"
                        set {_n} to name of event-slot
                        set {_a} to {_a} parsed as integer
                        set slot {_index} of event-inventory to createitem(paper,{_a},"&b選定 %{_n}%",{_lore::*},1,"",false)
                        set {lorechat::%player%::select} to index of event-slot
                        play sound "BLOCK.PISTON.EXTEND" with volume 1 with pitch 1.5 to player
                        clear {_lore::*}
                        loop 54 times:
                            set {_slot} to slot (loop-value)-1 of event-inventory
                            if "%type of {_slot}%" = "book":
                                set {_a} to "%item amount of {_slot}%"
                                set {_a} to {_a} parsed as integer
                                set {_n} to name of {_slot}
                                add "&7&m---------------------------------" to {_lore::*}
                                add "" to {_lore::*}
                                add line 3 of lore of {_slot} to {_lore::*}
                                add "" to {_lore::*}
                                add "&7[ &6左鍵 &e替換 這一行 &7]"  to {_lore::*}
                                add "&7[ &a右鍵 &e移動到 這一行 &7]"  to {_lore::*}
                                add "" to {_lore::*}
                                add "&7&m---------------------------------" to {_lore::*}
                                set slot (loop-value)-1 of event-inventory to createitem(string,{_a},{_n},{_lore::*},1,"",false)
                                clear {_lore::*}
                        set slot 53 of event-inventory to barrier named "&c取消移動" with lore ""
                    #插入行
                    else if click action is middle mouse button:
                        if slot 51 of event-inventory is air:
                            set {_select} to index of event-slot
                            set {_slot::0} to event-slot
                            set {_count} to 1
                            set {_nextitem} to slot {_select}+1 of event-inventory
                            if "%type of {_nextitem}%" = "air":
                                set slot {_select}+1 of event-inventory to event-slot
                            else:
                                while "%type of {_nextitem}%" != "air":
                                    set {_nextitem} to slot {_select}+{_count} of event-inventory

                                    set {_slot::%{_count}%} to slot {_select}+{_count} of event-inventory
                                    set {_v} to {_count}-1
                                    set slot {_count}+{_select} of event-inventory to {_slot::%{_v}%} 
                                    add 1 to {_count}
                            send "&7&m---------------------------------" 
                            send ""
                            send "&a成功插入一則空敘述於 第 &b%{_select}+1% &a行"
                            send ""
                            send "&7&m---------------------------------"  
                            play sound "BLOCK.ANVIL.USE" with volume 1 with pitch 1.5 to player
                            set line 3 of lore of slot {_select} of event-inventory to ""
                            itemloresave(player)
                        else:
                            send "&c無法添加過多Lore"
                    #刪除行
                    else if click action is right mouse button with shift:
                        set {_select} to index of event-slot
                        set {_nextitem} to slot {_select}+1 of event-inventory
                        set {_count} to 1
                        if "%type of {_nextitem}%" = "air":
                            set slot {_select} of event-inventory to air
                        else:
                            while "%type of {_nextitem}%" != "air":
                                set {_nextitem} to slot {_select}+{_count} of event-inventory
                                set slot {_count}+{_select}-1 of event-inventory to {_nextitem}
                                add 1 to {_count}
                        send "&7&m---------------------------------" 
                        send ""
                        send "&c成功刪除 第 &b%{_select}+1% &c行 敘述"
                        send ""
                        send "&7&m---------------------------------"  
                        play sound "ENTITY.BLAZE.DEATH" with volume 1 with pitch 1.5 to player
                        itemloresave(player)

                #移動行數
                else if "%type of event-slot%" is "string":
                    if click action is left mouse button:
                        
                        set {_slottemp} to slot {lorechat::%player%::select} of event-inventory
                        set slot {lorechat::%player%::select} of event-inventory to event-slot

                        set slot index of event-slot of event-inventory to {_slottemp}
                        
                        send "&7&m---------------------------------" 
                        send ""
                        send "&e成功將 第 &b%{lorechat::%player%::select}+1% &e行 的敘述與 第 &b%index of event-slot +1 % &e行互換"
                        send ""
                        send "&7&m---------------------------------"  
                        play sound "BLOCK.ANVIL.USE" with volume 1 with pitch 1.5 to player
                    else if click action is right mouse button:
                        set {_newslot} to index of event-slot
                        set {_slottmp} to slot {lorechat::%player%::select} of event-inventory
                        if {lorechat::%player%::select} < {_newslot}:
                            loop {_newslot}-{lorechat::%player%::select} times:
                                set {_slot} to slot (loop-value)+{lorechat::%player%::select} of event-inventory
                                set slot (loop-value)+{lorechat::%player%::select}-1 of event-inventory to {_slot}
                        else:
                            set {_slot::0} to event-slot
                            loop {lorechat::%player%::select}-{_newslot} times:
                                set {_slot::%loop-value%} to slot {_newslot}+loop-value of event-inventory
                                set {_v} to (loop-value)-1
                                set slot loop-value+{_newslot} of event-inventory to {_slot::%{_v}%} 
                        set slot {_newslot} of event-inventory to {_slottmp} 

                        send "&7&m---------------------------------" 
                        send ""
                        send "&e成功將 第 &b%{lorechat::%player%::select}+1% &e行 移動至 第 &b%index of event-slot +1 % &e行"
                        send ""
                        send "&7&m---------------------------------"  
                        play sound "BLOCK.ANVIL.USE" with volume 1 with pitch 1.5 to player

                    clear {lorechat::%player%::select}
                    itemloresave(player)
                        
                else if name of event-slot = "&c取消移動":
                    clear {lorechat::%player%::select}
                    lore_itemgui(player, lore of player's tool)
            else:
                if index of event-slot < 51:
                    if slot 51 of event-inventory is air:
                        add "" to lore of player's tool
                        play sound "BLOCK.ANVIL.USE" with volume 1 with pitch 1.5 to player
                        close player's inventory
                        lore_itemgui(player, lore of player's tool)

function lore_itemgui(p:player,lore:texts):
    OPEN chest inventory with 6 row named "&1lore管理" to {_p}
    set {_count} to 0
    loop {_lore::*}:
        add 1 to {_count}

        add "&7&m---------------------------------" to {_l::*}
        add "" to {_l::*}
        add "%loop-value%" to {_l::*}
        add "" to {_l::*}
        add "&7[ &6左鍵 &e編輯 這一行 &7]"  to {_l::*}
        add "&7[ &a右鍵 &e移動 這一行 &7]"  to {_l::*}
        add "&7[ &5點擊滑鼠滾輪 &f插入空行 &7]"  to {_l::*}
        add "&7[ &cshift+右鍵 刪除這行 &7]"  to {_l::*}
        add "&7[ &3點擊空格處可直接新增空描述 &7]"  to {_l::*} 
        add "&7&m---------------------------------" to {_l::*}

        set slot {_count}-1 of {_p}'s current inventory to  createitem(book,{_count},"&7第 &f%{_count}% &7行",{_l::*},1,"",false)
        clear {_l::*}
function itemloresave(p:player):
    loop 54 times:
        set {_count} to (loop-value)-1
        set {_slot} to slot {_count} of {_p}'s current inventory
        if {_slot} is set:
            add line 3 of lore of {_slot} to {_lore::*}
        else:
            stop loop
        clear {_slot}
    set lore of {_p}'s tool to {_lore::*}
    lore_itemgui({_p},lore of {_p}'s tool)

on unload:
    delete {lorechat::*}

on quit:
    delete {lorechat::%player%::*}

on chat:
    if {lorechat::%player%::line} is set:
        cancel event
        if message = "cancel":
            delete {lorechat::%player%::line}
            play sound "ENTITY.BLAZE.DEATH" with volume 1 with pitch 1.5 to player
            send "&C已取消輸入"
        else:
            set {_msg} to message
            replace all "&" with "§" in {_msg}
            set line {lorechat::%player%::line} of lore of player's tool to {_msg}
            send "&e成功將 第 %{lorechat::%player%::line}% 行 的敘述替換成 %{_msg}%"
            play sound "BLOCK.ANVIL.USE" with volume 1 with pitch 1.5 to player
            delete {lorechat::%player%::line}
        lore_itemgui(player,lore of player's tool)